autoload -Uz compinit
compinit

# Linuxbrew PATH設定
if [ "$(uname)" = "Linux" ] && [ -d "/home/linuxbrew/.linuxbrew" ]; then
    eval "$(/home/linuxbrew/.linuxbrew/bin/brew shellenv)"
fi

# Compression
compress() { tar -czf "${1%/}.tar.gz" "${1%/}"; }
alias decompress="tar -xzf"

# Convert webm files generated by the Gnome screenshot video recorder to mp4s that are more compatible
webm2mp4() {
  input_file="$1"
  output_file="${input_file%.webm}.mp4"
  ffmpeg -i "$input_file" -c:v libx264 -preset slow -crf 22 -c:a aac -b:a 192k "$output_file"
}

# Gitリポジトリに移動する(ghq list > cd)
cd-fzf-ghqlist() {
    local GHQ_ROOT=`ghq root`
    local REPO=`ghq list -p | sed -e 's;'${GHQ_ROOT}/';;g' |fzf +m`
    if [ -n "${REPO}" ]; then
        BUFFER="cd ${GHQ_ROOT}/${REPO}"
    fi
    zle accept-line
}
zle -N cd-fzf-ghqlist

# ghq projectをZellij Sessionで開く
prj () {
    local GHQ_ROOT=`ghq root`
    local repo_path=`ghq list -p | sed -e 's;'${GHQ_ROOT}/';;g' |fzf +m`
    if [ -z "${repo_path}" ]; then
      return
    fi

  if [ -f "${GHQ_ROOT}/${repo_path}/.workspace.kdl" ]; then
    local layout="${GHQ_ROOT}/${repo_path}/.workspace.kdl"
  else 
    local layout="default"
  fi
  # 元の文字列を生成
  local _raw_name="$(basename "$(dirname "$repo_path")")/$(basename "$repo_path")"
  # 置換（ . | / → _ ）
  local _sanitized="${_raw_name//[.|\/]/_}"
  # 先頭 36 文字だけ取り出す
  local session_name="${_sanitized:0:36}"

  echo cwd: ${GHQ_ROOT}/${repo_path}
  echo session-name: ${session_name}
  # ── 同名セッションが無ければバックグラウンド生成
  if ! zellij list-sessions --no-formatting | grep -qx "$session_name"; then
    echo 1
    zellij attach "$session_name" --create-background \
      options --default-cwd ${GHQ_ROOT}/${repo_path} --default-layout $layout
  fi

  # Zellij 内ならプラグインに切替メッセージを送る
  if [[ -n $ZELLIJ ]]; then
    zellij pipe --plugin "https://github.com/mostafaqanbaryan/zellij-switch/releases/download/0.2.1/zellij-switch.wasm" -- "--session $session_name"
  else
    zellij attach "$session_name" -c \
      options --default-cwd ${GHQ_ROOT}/${repo_path} --default-layout $layout
  fi

}

# Gitブランチを切り替えする(git branch > git checkout)
function checkout-fzf-gitbranch() {
    local GIT_BRANCH=$(git branch --all | grep -v HEAD | fzf +m)
    if [ -n "$GIT_BRANCH" ]; then
        git checkout $(echo "$GIT_BRANCH" | sed "s/.* //" | sed "s#remotes/[^/]*/##")
    fi
    zle accept-line
}
zle -N checkout-fzf-gitbranch

# Gitブランチ指定を楽にする
alias -g B='`git branch --all | grep -v HEAD | fzf -m | sed "s/.* //" | sed "s#remotes/[^/]*/##"`'

# 登録サーバにSSH(cat ~/.ssh/config > ssh)
function ssh-fzf-sshconfig() {
    local SSH_HOST=$(awk '
        tolower($1)=="host" {
            for (i=2; i<=NF; i++) {
                if ($i !~ "[*?]") {
                    print $i
                }
            }
        }
    ' ~/.ssh/config | sort | fzf +m)
    if [ -n "$SSH_HOST" ]; then
        BUFFER="ssh $SSH_HOST"
    fi
    zle accept-line
}
zle -N ssh-fzf-sshconfig

bindkey '^G' cd-fzf-ghqlist
bindkey '^O' checkout-fzf-gitbranch
bindkey '^\' ssh-fzf-sshconfig

# for lazygit
export XDG_CONFIG_HOME="$HOME/.config"

[ -f ~/.zshrc.local ] && source ~/.zshrc.local

if command -v starship &> /dev/null; then
  eval "$(starship init zsh)"
fi

if command -v mise &> /dev/null; then
  eval "$(mise activate zsh)"
fi

if command -v sheldon &> /dev/null; then
  eval "$(sheldon source)"
fi

if command -v atuin &> /dev/null; then
  eval "$(atuin init zsh --disable-up-arrow)"
fi

if command -v zabrze &> /dev/null; then
  eval "$(zabrze init --bind-keys)"
fi

# vscode shell integration manual install
[[ "$TERM_PROGRAM" == "vscode" ]] && . "$(code --locate-shell-integration-path zsh)"

export PATH="/opt/homebrew/opt/libpq/bin:$PATH"

export PATH="$HOME/.local/bin:$PATH"
